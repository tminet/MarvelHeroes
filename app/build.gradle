plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
}

apply {
    from '../config/detekt/detekt.gradle'
}

def signingProperties = new Properties()
signingProperties.load(new FileInputStream(rootProject.file("signing.properties")))

def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(rootProject.file("apikey.properties")))

android {
    compileSdk 31

    signingConfigs {
        release {
            storeFile file("$rootDir/${signingProperties['STORE_FILE']}")
            storePassword signingProperties['STORE_PASSWORD']
            keyAlias signingProperties['KEY_ALIAS']
            keyPassword signingProperties['KEY_PASSWORD']
        }
    }

    defaultConfig {
        applicationId "tmidev.marvelheroes"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "BASE_URL", '"https://gateway.marvel.com/v1/public/"'
        buildConfigField "String", "PUBLIC_KEY", apikeyProperties['PUBLIC_KEY']
        buildConfigField "String", "PRIVATE_KEY", apikeyProperties['PRIVATE_KEY']

        testInstrumentationRunner "tmidev.marvelheroes.CustomTestRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            shrinkResources false
            debuggable true
        }

        staging {
            initWith debug
            applicationIdSuffix ".staging"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro', 'proguard-rules-staging.pro'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation project(':core')

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'

    implementation 'com.google.android.material:material:1.5.0'

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_ver"

    implementation "androidx.navigation:navigation-ui-ktx:$navigation_ver"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_ver"

    kapt "com.google.dagger:hilt-android-compiler:$hilt_ver"
    implementation "com.google.dagger:hilt-android:$hilt_ver"

    kapt "androidx.room:room-compiler:$room_ver"
    implementation "androidx.room:room-ktx:$room_ver"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_ver"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_ver"

    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.0"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    kapt "com.github.bumptech.glide:compiler:$glide_ver"
    implementation "com.github.bumptech.glide:glide:$glide_ver"

    implementation "androidx.paging:paging-runtime-ktx:$paging_ver"

    implementation 'androidx.datastore:datastore-preferences:1.0.0'

    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    testImplementation project(":testing")
    testImplementation "androidx.room:room-testing:$room_ver"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'

    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_ver"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_ver"

    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.9.3'

    debugImplementation 'androidx.fragment:fragment-testing:1.4.1'
}